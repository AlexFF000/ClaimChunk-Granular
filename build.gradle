import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
}

// Plugin information
group 'com.cjburkey'
version '0.0.21-SNAPSHOT'
project.ext.liveVersion = '0.0.20'
project.ext.pluginName = 'ClaimChunk'
project.ext.mainClass = 'com.cjburkey.claimchunk.ClaimChunk'

// Dependency versions
project.ext.bukkitVersion = '1.15.2-R0.1-SNAPSHOT'
project.ext.spigotVersion = '1.15.2-R0.1-SNAPSHOT'
project.ext.vaultApiVersion = '1.7'
project.ext.worldEditCoreVersion = '7.1.0-SNAPSHOT'
project.ext.worldGuardVersion = '7.0.2-SNAPSHOT'
project.ext.worldGuardLegacyVersion = '7.0.0-SNAPSHOT'

// Use `gradlew jar` just to build the jar file.
// Use `gradlew copyClaimChunkToPluginsDir` to build the jar file and copy the plugin into the /plugins/ directory
// To use these tasks effectively, download Spigot to this folder.
project.ext.testServerDir = new File('./run/')

// Java 8
sourceCompatibility = 1.8

project.ext.readmeIn = './unbuilt_readme.md'
project.ext.readmeOut = './README.md'

// Tokens to replace within files
project.ext.tokens = [
        "PLUGIN_VERSION": project.version,
        "MAIN_CLASS"    : project.ext.mainClass,
        "PLUGIN_NAME"   : project.ext.pluginName,
        "LIVE_VERSION"  : project.ext.liveVersion,
        "SPIGOT_VERSION": project.ext.spigotVersion.substring(0, project.ext.spigotVersion.indexOf('-')),
]

// Extra repos for Bukkit/Spigot stuff
repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'http://nexus.hc.to/content/repositories/pub_releases/' }
    maven { url 'https://maven.sk89q.com/repo/' }
    maven { url 'https://repo.mikeprimm.com' }
    maven { url 'https://papermc.io/repo/repository/maven-public/' }
}

dependencies {
    implementation "org.bukkit:bukkit:$bukkitVersion"
    implementation "org.spigotmc:spigot-api:$spigotVersion"
    implementation "net.milkbowl.vault:VaultAPI:$vaultApiVersion"
    implementation "com.sk89q.worldedit:worldedit-core:$worldEditCoreVersion"
    implementation "com.sk89q.worldguard:worldguard-legacy:$worldGuardLegacyVersion"
}

// Enable all compiler warnings for cleaner (hopefully) code
compileJava {
    options.warnings = true
    options.deprecation = true
    options.compilerArgs += '-Xlint:all'
}

clean {
    file(project.ext.readmeOut).delete()
}

// Fill in readme placeholders
task updateReadme {
    inputs.file project.ext.readmeIn
    outputs.file project.ext.readmeOut

    doLast {
        copy {
            from project.ext.readmeIn
            into file(project.ext.readmeOut).getParent()
            rename { n -> file(project.ext.readmeOut).getName() }
            filter ReplaceTokens, tokens: project.ext.tokens
        }
    }
}

clean.finalizedBy updateReadme

// Replace placeholders with values in source and resource files
processResources {
    filter ReplaceTokens, tokens: project.ext.tokens
}

// Copy from the libs dir to the plugins directory in the testServerDir
task copyClaimChunkToPluginsDir(type: Copy) {
    dependsOn jar
    from file(jar.outputs.files.singleFile)
    into file(new File(testServerDir, '/plugins/'))
    rename 'claimchunk-(.*).jar', 'claimchunk.jar'
}
