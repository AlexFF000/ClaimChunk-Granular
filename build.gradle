plugins {
    id 'java'
}

group 'com.cjburkey'
version '0.0.20-SNAPSHOT'

project.ext.bukkitVersion = '1.15-R0.1-SNAPSHOT'
project.ext.spigotVersion = '1.15-R0.1-SNAPSHOT'
project.ext.vaultApiVersion = '1.7'
project.ext.worldEditCoreVersion = '7.1.0-SNAPSHOT'
project.ext.worldGuardVersion = '7.0.2-SNAPSHOT'
project.ext.worldGuardLegacyVersion = '7.0.0-SNAPSHOT'

// Use `gradlew jar` just to build the jar file.
// Use `gradlew copyClaimChunkToPluginsDir` to build the jar file and copy the plugin into the /plugins/ directory
// Use `gradlew buildPluginAndRunServer` to execute `copyClaimChunkToPluginsDir` and run the server
// To use these tasks, download Spigot to this file.
project.ext.spigotFile = new File('./run/spigot-1.14.4.jar')

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'http://nexus.hc.to/content/repositories/pub_releases/' }
    maven { url 'https://maven.sk89q.com/repo/' }
    maven { url 'https://repo.mikeprimm.com' }
    maven { url 'https://papermc.io/repo/repository/maven-public/' }
}

dependencies {
    implementation "org.bukkit:bukkit:$bukkitVersion"
    implementation "org.spigotmc:spigot-api:$spigotVersion"
    implementation "net.milkbowl.vault:VaultAPI:$vaultApiVersion"
    implementation "com.sk89q.worldedit:worldedit-core:$worldEditCoreVersion"
    implementation "com.sk89q.worldguard:worldguard-legacy:$worldGuardLegacyVersion"
}

compileJava {
    options.warnings = true
    options.deprecation = true
    options.compilerArgs += '-Xlint:all'
}

task copyClaimChunkToPluginsDir(type: Copy) {
    dependsOn jar
    from file(jar.outputs.files.singleFile)
    into file(new File(spigotFile.getParent(), '/plugins/'))
    rename 'claimchunk-(.*).jar', 'claimchunk.jar'
}

task buildPluginAndRunServer() {
    dependsOn copyClaimChunkToPluginsDir
    doLast {
        exec {
            workingDir = spigotFile.getParentFile()
            executable = new File(System.properties.'java.home' as String, '/bin/java')
            args += [
                    '-jar',
                    '-Xms3G',
                    '-Xmx3G',
                    '-DIReallyKnowWhatIAmDoingISwear',
                    spigotFile.getName()
            ]
        }
    }
}
